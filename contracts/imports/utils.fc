(int) power(int x, int n) {
    if (n == 1) {
        return x;
    }
    if (n == 0) {
        return 1;
    }

    int x1 = power(x, (n / 2));
    x1 *= x1;
    if (n % 2) {
        x1 *= x;
    }
    return x1;
}

;; Decodings:
;;   3 bits - base_factor, where base = 10/8
;;   4 bits - power of 10
;;
;; Examples:
;;     0x30 -> 00110 000 -> 0.00100 TON
;;     0x31 -> 00110 001 -> 0.00125 TON
;;     0x32 -> 00110 010 -> 0.00250 TON
;;     0x33 -> 00110 011 -> 0.00375 TON
;;     0x34 -> 00110 100 -> 0.00500 TON
;;     0x35 -> 00110 101 -> 0.00625 TON
;;     0x36 -> 00110 110 -> 0.00750 TON
;;     0x37 -> 00110 111 -> 0.00875 TON
;;     0x38 -> 00111 000 -> 0.01000 TON
;;     0x39 -> 00111 001 -> 0.01250 TON
;;     0x3A -> 00111 010 -> 0.02500 TON
;;     0x3B -> 00111 011 -> 0.03750 TON
;;     0x3C -> 00111 100 -> 0.05000 TON
;;     0x3D -> 00111 101 -> 0.06250 TON
;;     0x3E -> 00111 110 -> 0.07500 TON
;;     0x3F -> 00111 111 -> 0.08750 TON
;;     0x40 -> 01000 000 -> 0.10000 TON
;;     0x41 -> 01000 001 -> 0.12500 TON
;;     0x42 -> 01000 010 -> 0.25000 TON
;;     0x43 -> 01000 011 -> 0.37500 TON
;;     0x44 -> 01000 100 -> 0.50000 TON
;;     0x45 -> 01000 101 -> 0.62500 TON
;;     0x46 -> 01000 110 -> 0.75000 TON
;;     0x47 -> 01000 111 -> 0.87500 TON
;;     0x48 -> 01001 000 -> 1.00000 TON
;;

(int) unpack_qint(int qnum) {
    qnum &= 0x7F;
    if (qnum == 0) {
        return 0;
    }

    int num = ((qnum & 7) * 10);
    num = num ? num : 8;
    return (num * power(10, qnum >> 3)) >> 3;
}

(int) random_uint64() {
    return random() & 0xFFFFFFFFFFFFFFFF;
}

(int) random_uint32() {
    return random() & 0xFFFFFFFF;
}

;; BCD encoded date mask
;; 4 octets - year; 2 Octets - month; 2 octets - day; 0x00 - means unspecified or unknown
;;   0x20250100 - means 2025-01-*
;;   0x20250000 - means 2025-*
(slice) date_mask_to_string(int n) {
    builder string = begin_cell();

    if ((n >> 16) & 0xFFFF) {
        int i = 32;
        do {
            i -= 4;
            string~store_uint(((n >> i) & 0xF) + 48, 8);
            if ((i == 16) | (i == 8)) {
                string~store_uint(45, 8); ;; "-"
                if ((n >> (i - 8)) & 0xFF == 0) {
                    string~store_uint(42, 8); ;; "*"
                    i = 0;
                }
            }
        } until (i == 0);
    }
    else {
        string~store_uint(42, 8); ;; "*"
    }

    return string.end_cell().begin_parse();
}

;; BCD 5-bits encoded 2-letter english char code
;;  used for ISO 639 two letter language code or ISO 3166-1 alpha-2 code
(slice) bcd2c_to_string(int n) {
    builder string = begin_cell();
    string~store_uint(((n >> 5) & 0x1F) + 65, 8);
    string~store_uint((n & 0x1F) + 65, 8);
    return string.end_cell().begin_parse();
}